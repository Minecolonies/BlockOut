configurations {
    contained {}
    compile.extendsFrom contained
}

dependencies {
    contained ('com.google.inject:guice:4.2.1')
    contained 'com.intellij:annotations:+@jar'
    contained ('com.esotericsoftware:kryo:4.+') {
        exclude group: 'org.ow2.asm', module: 'asm'
    }
    
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'org.apache.logging.log4j:log4j-api:2.8.1'
    compile 'com.google.code.gson:gson:2.8.0' 
}

jar {
    from(configurations.contained.files) {
        include '*'
        into 'META-INF/libraries'
    }

    manifest {
        attributes([
                'ContainedDeps': configurations.contained.files.collect { it.name }.join(' '),
                'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}:api",
                'Timestamp': System.currentTimeMillis(),
        ])
    }

    classifier 'api'
}

