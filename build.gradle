//We need this in the parent project since it else not discovered by the subprojects.
buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:latest.release"
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.1.0"
}

//Now configure all subprojects.
allprojects {
    //Add the central repository so that we always have access to our cached dependencies.
    repositories {
        maven {
            name 'LDTTeam - Modding'
            url 'https://ldtteam.jfrog.io/ldtteam/modding/'
        }
    }

    //Setup the plugins that we need.
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'com.jfrog.artifactory'
    apply plguin: 'maven'

    //Force minecraft compatible versions of dependencies
    configurations.all {
        resolutionStrategy {
            force 'com.google.guava:guava:21.0'
        }
    }

    //All mods have the same test harness.
    //JUNIT, MOCKITO, POWERMOCK and HAMCREST are being added here.
    dependencies {
        testCompile 'junit:junit:4.11'
        testCompile "org.mockito:mockito-core:1.+"
        testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.6.5'
        testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: '1.6.5'
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testCompile group: 'pl.pragmatists', name:'JUnitParams', version: '1.1.1'

        compile 'com.intellij:annotations:+@jar'

        compile (group: 'com.ldtteam', name: 'JVoxelizer', version: jvoxelizer_version, classifier: 'api') {
            transitive = false;
        }
    }

    group = "com.ldtteam"
    version = minecraft_version.toString() + "-" + System.getenv().get("Version")
    archivesBaseName = "blockout"

    //Setup for which java version we are compiling
    //Minecraft defaults to 1.8, anything higher does currently not work with Forge.
    sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
    compileJava {
        sourceCompatibility = targetCompatibility = '1.8'
    }

    //Setup the compiler to output all information we need.
    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint"
    }

    //Create a javadoc jar
    task javadocJar(type: Jar, dependsOn: 'javadoc') {
        from "build/docs/javadoc"
        classifier "javadoc"
    }

    //The primary jar needs to have a javadoc jar build
    //Ensures it is created and ready to upload.
    jar {
        dependsOn tasks.javadocJar
    }
}

subprojects {
    artifacts {
        archives jar
        archives javadocJar
    }
}


/*
project('guidebook') {
    dependencies {
        compile project(':main')
        compile project(':utilities')
    }
}*/